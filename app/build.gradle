apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
//定义打包时间函数
static def releaseTime() {
    return new Date().format("yyyyMMddHHmm", TimeZone.getDefault())
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.candy.capture"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        manifestPlaceholders = [ CHANNEL_NAME:"Google"]

        multiDexEnabled true

        //指定room.schemaLocation生成的文件路径
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildFeatures.dataBinding = true

    // 签名配置
    signingConfigs {
        dev {
            storeFile file("../zhanghq.jks")
            storePassword "zhanghq123456"
            keyAlias "zhanghq"
            keyPassword "zhanghq123456"
        }

        release {
            storeFile file("../zhanghq.jks")
            storePassword "zhanghq123456"
            keyAlias "zhanghq"
            keyPassword "zhanghq123456"
        }
    }

    buildTypes {
        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"

            versionNameSuffix ""
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.dev
        }

        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            // 是否混淆
            minifyEnabled true
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }


    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    flavorDimensions "channel"
    productFlavors {
        google {
            manifestPlaceholders = [ CHANNEL_NAME:"GooglePlay"]
        }
        yingyongbao {
            manifestPlaceholders = [ CHANNEL_NAME:"YingYongBao"]
        }
        wandoujia {
            manifestPlaceholders = [ CHANNEL_NAME:"WanDouJia" ]
        }
        baidu {
            manifestPlaceholders = [ CHANNEL_NAME:"BaiDu" ]
        }
        xiaomi {
            manifestPlaceholders = [ CHANNEL_NAME:"XiaoMi" ]
        }
        _360 {
            manifestPlaceholders = [ CHANNEL_NAME:"360" ]
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    applicationVariants.all { variant ->
        def versionName = variant.getVersionName()
        def isRelease = variant.getBuildType().isMinifyEnabled()
        variant.outputs.all {
            if (isRelease) {
                outputFileName = "capture_v${versionName}_${variant.getFlavorName()}_${releaseTime()}.apk"
            }
        }
    }
}

configurations.all {
    resolutionStrategy {
        force("org.antlr:antlr4-runtime:4.5.3")
        force("org.antlr:antlr4-tool:4.5.3")
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.umeng.analytics:analytics:6.1.4'
    implementation 'com.jaeger.statusbarutil:library:1.5.1'
    implementation project(path: ':commonLibrary')
    implementation project(path: ':videoPlayer')
    implementation "androidx.core:core-ktx:$core_ktx_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9'
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    implementation "androidx.paging:paging-runtime-ktx:$page_version"
    implementation "androidx.datastore:datastore-preferences:1.0.0-alpha02"
}
