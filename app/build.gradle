apply plugin: 'com.android.application'
//定义打包时间函数
def releaseTime() {
    return new Date().format("yyyyMMddHHmm", TimeZone.getDefault())
}
// 将打的包按日期归类
def releaseDate() {
    return new Date().format("yyyyMMdd", TimeZone.getDefault())
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"
    defaultConfig {
        applicationId "com.candy.capture"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [ CHANNEL_NAME:"Google"]

        multiDexEnabled true
    }

    // 签名配置
    signingConfigs {
//        debug {
//            storeFile file("/zhanghq.jks")
//            storePassword "zhanghq123456"
//            keyAlias "zhanghq"
//            keyPassword "zhanghq123456"
//        }

        release {
            storeFile file("/zhanghq.jks")
            storePassword "zhanghq123456"
            keyAlias "zhanghq"
            keyPassword "zhanghq123456"
        }
    }

    buildTypes {
        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"

            versionNameSuffix ""
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }

        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            // 是否混淆
            minifyEnabled true
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }


    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    productFlavors {
        google {
            manifestPlaceholders = [ CHANNEL_NAME:"GooglePlay"]
        }
        yingyongbao {
            manifestPlaceholders = [ CHANNEL_NAME:"YingYongBao"]
        }
        wandoujia {
            manifestPlaceholders = [ CHANNEL_NAME:"WanDouJia" ]
        }
        baidu {
            manifestPlaceholders = [ CHANNEL_NAME:"BaiDu" ]
        }
        xiaomi {
            manifestPlaceholders = [ CHANNEL_NAME:"XiaoMi" ]
        }
        _360 {
            manifestPlaceholders = [ CHANNEL_NAME:"360" ]
        }
    }

    applicationVariants.all { variant ->
        def versionName = variant.getVersionName()
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk') && 'release'.equals(variant.buildType.name)) {
                def fileName = "capture_v${versionName}_${variant.productFlavors[0].name}_${releaseTime()}.apk"
                def filePath = "${outputFile.parent}\\${releaseDate()}"
                output.outputFile = new File(filePath, fileName)
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.android.support:cardview-v7:25.0.0'
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile 'com.android.support:support-v4:24.2.1'
    testCompile 'junit:junit:4.12'
}
